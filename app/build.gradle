import java.text.SimpleDateFormat

plugins {
	id "com.android.application"
	id "com.google.gms.google-services"
	id "com.google.firebase.crashlytics"
	id "com.facebook.react"
}

android {
	namespace "com.mrboomdev.platformer"
	ndkVersion rootProject.ext.ndkVersion
	
	sourceSets {
		main {
			manifest.srcFile "AndroidManifest.xml"
			java.srcDirs = ["src/main/java"]
			aidl.srcDirs = ["src/main/java"]
			renderscript.srcDirs = ["src/main/java"]
			res.srcDirs = ["res"]
			assets.srcDirs = ["../assets", "src/main/assets"]
			jniLibs.srcDirs = ["libs"]
		}
	}
	
	defaultConfig {
		applicationId "com.mrboomdev.platformer"
		versionName "0.5.0"
		versionCode 13

		minSdkVersion rootProject.ext.minSdkVersion
		targetSdkVersion rootProject.ext.targetSdkVersion
		compileSdk rootProject.ext.compileSdkVersion
		multiDexEnabled true

		var formatter = new SimpleDateFormat()
		formatter.applyPattern("dd-MM-yyyy, HH:mm")

		buildConfigField("String", "BUILD_TIME", "\"" + formatter.format(new Date()) + "\"")

		buildFeatures {
			renderScript = true
			buildConfig = true
			aidl = true
		}

        externalNativeBuild {
            cmake {
                cppFlags ''
            }
        }

		packagingOptions {
			pickFirst "META-INF/AL2.0"
			pickFirst "META-INF/LGPL2.1"
			pickFirst "xsd/catalog.xml"

			merge "META-INF/INDEX.LIST"

			merge "META-INF/DEPENDENCIES"

			merge "META-INF/LICENSE-notice.md"
			merge "META-INF/LICENSE.md"
			merge "META-INF/LICENSE"
			merge "META-INF/licenses"

			merge "META-INF/NOTICE"
			merge "META-INF/NOTICE.md"

			exclude "com/badlogic/**"
			exclude "bsh/util/**"
			exclude "bsh/servlet/*"
			exclude "images/ant_logo_large.gif"
			exclude "license/**"
		}
    }
	
	splits {
		abi {
			reset()
			enable false
			universalApk false
			//noinspection ChromeOsAbiSupport
			include project.getProperties().get("reactNativeArchitectures")
		}
	}
	
	signingConfigs {
		debug {
			storeFile file("debug.keystore")
			storePassword "android"
			keyAlias "androiddebugkey"
			keyPassword "android"
		}
	}
	
	compileOptions {
		sourceCompatibility "11.0"
		targetCompatibility "11.0"
		coreLibraryDesugaringEnabled true
	}
	
	buildTypes {
		debug {
			applicationIdSuffix ".debug"
			versionNameSuffix "-debug"
			signingConfig signingConfigs.debug
            jniDebuggable true
        }
		
		release {
			versionNameSuffix "-release"
			signingConfig signingConfigs.debug
			minifyEnabled false
			shrinkResources false
			proguardFiles getDefaultProguardFile("proguard-android.txt"), "proguard-rules.pro"
		}
	}

    externalNativeBuild {
        cmake {
            path file('src/main/cpp/CMakeLists.txt')
            version '3.22.1'
        }
    }
}

repositories {
	google()
}

configurations { natives }

dependencies {
	/* Local libraries */
	implementation fileTree(dir: "libs", include: ["*.jar", "*.aar"])
	implementation files("../epic/Bin/Android/static-stdc++/aar/eos-sdk.aar")

	/* Due to some crazy shit, the compilation is broken on new versions
	   noinspection GradleDependency */
	coreLibraryDesugaring "com.android.tools:desugar_jdk_libs:1.1.8"
	
	/* Firebase */
	implementation platform("com.google.firebase:firebase-bom:31.1.1")
	implementation "com.google.firebase:firebase-crashlytics"
	implementation "com.google.firebase:firebase-messaging"
	implementation "com.google.firebase:firebase-analytics"
	implementation "com.google.android.gms:play-services-auth:20.7.0"
	
	/* Util libraries */
	implementation 'com.squareup.moshi:moshi:1.15.0'
	implementation "com.squareup.okhttp3:okhttp:5.0.0-alpha.11"
	implementation "org.apache-extras.beanshell:bsh:2.0b6"
	implementation "org.apache.ant:ant:1.10.12"

	implementation ("com.android.tools.build:builder:8.1.2") {
		exclude group: "com.sun.activation", module: "javax.activation"
	}
	
	/* Android libraries */
	implementation "com.google.android.material:material:1.9.0"
	implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.1.0"
	implementation "androidx.constraintlayout:constraintlayout:2.1.4"
	implementation "androidx.security:security-crypto:1.0.0"
	implementation "androidx.browser:browser:1.4.0"
	
	/* React Native */
	implementation "com.facebook.react:react-android"
	implementation "com.facebook.react:hermes-android"

	debugImplementation("com.facebook.flipper:flipper:${FLIPPER_VERSION}")
	debugImplementation("com.facebook.flipper:flipper-fresco-plugin:${FLIPPER_VERSION}")

	debugImplementation("com.facebook.flipper:flipper-network-plugin:${FLIPPER_VERSION}") {
		exclude group:'com.squareup.okhttp3', module:'okhttp'
	}
	
	/* LibGDX libraries */
	api "com.badlogicgames.gdx:gdx:$gdxVersion"
	implementation 'com.badlogicgames.gdx:gdx-ai:1.8.2'
	implementation "com.badlogicgames.gdx:gdx-backend-android:$gdxVersion"

	natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-armeabi-v7a"
	natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-arm64-v8a"
	natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86"
	natives "com.badlogicgames.gdx:gdx-platform:$gdxVersion:natives-x86_64"

	implementation "com.badlogicgames.gdx:gdx-box2d:$gdxVersion"
	implementation 'com.badlogicgames.box2dlights:box2dlights:1.5'
	natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-armeabi-v7a"
	natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-arm64-v8a"
	natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-x86"
	natives "com.badlogicgames.gdx:gdx-box2d-platform:$gdxVersion:natives-x86_64"
	 
	implementation "com.badlogicgames.gdx:gdx-freetype:$gdxVersion"
	natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-armeabi-v7a"
	natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-arm64-v8a"
	natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-x86"
	natives "com.badlogicgames.gdx:gdx-freetype-platform:$gdxVersion:natives-x86_64"
}

tasks.register("copyDownloadableDepsToLibs", Copy) {
	from configurations.implementation
	into "libs"
}

tasks.register("copyAndroidNatives") {
	doFirst {
		file("libs/armeabi/").mkdirs()
		file("libs/armeabi-v7a/").mkdirs()
		file("libs/arm64-v8a/").mkdirs()
		file("libs/x86_64/").mkdirs()
		file("libs/x86/").mkdirs()

		configurations.getByName("natives").copy().files.each { jar ->
			def outputDir = null
			if(jar.name.endsWith("natives-arm64-v8a.jar")) outputDir = file("libs/arm64-v8a")
			if(jar.name.endsWith("natives-armeabi-v7a.jar")) outputDir = file("libs/armeabi-v7a")
			if(jar.name.endsWith("natives-armeabi.jar")) outputDir = file("libs/armeabi")
			if(jar.name.endsWith("natives-x86_64.jar")) outputDir = file("libs/x86_64")
			if(jar.name.endsWith("natives-x86.jar")) outputDir = file("libs/x86")

			if(outputDir != null) {
				copy {
					from zipTree(jar)
					into outputDir
					include "*.so"
				}
			}
		}
	}
}

tasks.configureEach { packageTask ->
	if(packageTask.name.contains("package")) {
		packageTask.dependsOn "copyAndroidNatives"
	}
}

tasks.register("run", Exec) {
	def path
	def localProperties = project.file("../local.properties")
	if(localProperties.exists()) {
		Properties properties = new Properties()
		localProperties.withInputStream { instr ->
			properties.load(instr)
		}

		def sdkDir = properties.getProperty("sdk.dir")
		if(sdkDir) {
			path = sdkDir
		} else {
			path = "$System.env.ANDROID_HOME"
		}
	} else {
		path = "$System.env.ANDROID_HOME"
	}

	def adb = path + "/platform-tools/adb"
	def activity = "com.mrboomdev.platformer/com.mrboomdev.platformer.GameDebugLauncher"

	commandLine "$adb", "shell", "am", "start", "-n", "$activity"
}

eclipse.project.name = "Binacty" + "-android"
apply from: file("../../node_modules/@react-native-community/cli-platform-android/native_modules.gradle")
applyNativeModulesAppBuildGradle(project)